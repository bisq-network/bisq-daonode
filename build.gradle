plugins {
    id 'application'
    id 'bisq.post-build'
    alias(libs.plugins.openjfx)
    alias(libs.plugins.shadow)
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

group 'bisq'
version = file("bisq/desktop/src/main/resources/version.txt").text.trim()

application {
    project.mainClassName = 'bisq.daonode.DaoNodeRestApiApplication'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

jar.manifest.attributes(
        "Implementation-Title": project.name,
        "Implementation-Version": version
)

configurations.all {
    exclude group: 'com.github.bisq-network:bitcoinj'
    exclude group: 'io.grpc'
}

dependencies {
    implementation enforcedPlatform(project(':platform'))

    implementation 'bisq:assets'
    implementation 'bisq:common'
    implementation('bisq:core') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    implementation 'bisq:p2p'

    implementation libs.chimp.jsocks
    implementation bisqLibs.fxmisc.easybind
    implementation libs.glassfish.jaxb
    implementation bisqLibs.google.guava
    implementation bisqLibs.google.guice

    implementation libs.bundles.jersey.libs
    implementation libs.swagger

    implementation bisqLibs.logback.core
    implementation bisqLibs.logback.classic
    compileOnly bisqLibs.lombok
    annotationProcessor bisqLibs.lombok
    implementation bisqLibs.slf4j.api

    testAnnotationProcessor bisqLibs.lombok
    testCompileOnly bisqLibs.lombok
    testImplementation libs.junit.jupiter
}

clean.doFirst {
    // Remove the project root level 'bisq-*' scripts and 'lib' dir generated at the end of the assemble task.
    delete fileTree(dir: projectDir, include: ['bisq-daonode', 'bisq-daonode.bat']), 'lib'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

distZip.enabled = false
distTar.enabled = false
shadowDistZip.enabled = false
shadowDistTar.enabled = false
shadowJar.enabled = false